import com.android.build.api.dsl.ManagedVirtualDevice

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'com.google.firebase.crashlytics'

def keyStorePropertiesFile = rootProject.file("keystore.properties")
def keyStoreProperties = new Properties()

keyStoreProperties.load(new FileInputStream(keyStorePropertiesFile))

android {
    testOptions {
        managedDevices {
            devices {
                nexus5api30(ManagedVirtualDevice) {
                    device = "Nexus 5"
                    apiLevel = 30
                    systemImageSource = "aosp-atd"
                    require64Bit = false
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    signingConfigs {
        config {
            keyAlias keyStoreProperties['KEY_ALIAS']
            keyPassword keyStoreProperties['KEY_PASSWORD']
            storeFile file(keyStoreProperties['STORE_FILE'])
            storePassword keyStoreProperties['KEYSTORE_PASSWORD']
        }
    }
    defaultConfig {
        applicationId "jermaine.shotclockapp"
        minSdk 23
        targetSdk 34
        compileSdk 34
        versionCode 6
        versionName "1.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    namespace 'jermaine.shotclockapp'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"

    implementation platform('com.google.firebase:firebase-bom:26.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'

    // Jetpack Compose
    implementation "androidx.compose.compiler:compiler:$composeCompilerVersion"
    implementation "androidx.compose.runtime:runtime:$composeRuntimeVersion"
    implementation "androidx.compose.ui:ui-tooling:$composeUiVersion"
    implementation "androidx.compose.ui:ui:$composeUiVersion"
    implementation "androidx.compose.material:material:$composeMaterialVersion"
    implementation "androidx.compose.foundation:foundation:$composeFoundationVersion"
    implementation "androidx.navigation:navigation-compose:2.7.6"
    implementation "com.google.accompanist:accompanist-pager:0.24.3-alpha"
    implementation "androidx.activity:activity-compose:1.8.2"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.23.1"

    // Test rules and transitive dependencies:
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$composeUiVersion"
    androidTestImplementation "androidx.compose.ui:ui-test:$composeUiVersion"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$composeUiVersion"

    // Preferences DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=com.google.accompanist.pager.ExperimentalPagerApi"
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.FlowPreview"
        freeCompilerArgs += "-opt-in=kotlin.time.ExperimentalTime"
    }
}